plugins {
    id 'antlr'
    id 'java'
}

group 'de.uni-trier.dbis.schenql'
version '1.0-SNAPSHOT'

sourceCompatibility = 11.0

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    antlr "org.antlr:antlr4:4.7.2"
    compile(
            "org.antlr:antlr4-runtime:4.7.2",
            "commons-cli:commons-cli:1.4",
            "mysql:mysql-connector-java:5.1.6"
    )
    testCompile 'junit:junit:4.12'
}

generateGrammarSource {
    maxHeapSize = "64m"
    arguments = [
            "-lib", "src/main/antlr/de/unitrier/dbis/schenql",
            "-visitor"
    ]
}
compileJava.dependsOn generateGrammarSource

jar {
    manifest {
        attributes "Main-Class": "de.unitrier.dbis.schenql.compiler.Schenql"
    }

    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

task customFatJar(type: Jar) {
    manifest {
        attributes "Main-Class": "de.unitrier.dbis.schenql.compiler.Schenql"
    }

    baseName = "schenql-full"

    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}